plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.0" apply false
    id "org.jetbrains.kotlinx.kover" version "0.4.4" apply false
    id "org.jetbrains.dokka" version "1.6.0" apply false
    id "org.sonarqube" version "3.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "hwolf_validation"
        property "sonar.organization", "hwolf"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportPaths", "**/build/test-results/**/*.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "**/build/kover/result.xml"
    }
}

group = 'hwolf'

subprojects {

    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlinx.kover"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "org.sonarqube"
    apply plugin: "maven-publish"

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
    }

    dependencies {
        implementation(platform("org.jetbrains.kotlin:kotlin-bom"))

        testImplementation(platform("org.junit:junit-bom:5.8.2"))
        testImplementation(platform("io.strikt:strikt-bom:0.33.0"))

        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "dev.minutest:minutest:2.0.0-rc4"
        testImplementation "io.strikt:strikt-core"
        testImplementation "io.mockk:mockk:1.12.1"
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "17"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "17"
        }
    }

    test {
        useJUnitPlatform()
        kover {
            enabled = true
        }
    }
    tasks.koverXmlReport {
        enabled = true
        xmlReportFile.set(layout.buildDirectory.file("kover/result.xml"))
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.javadocJar {
        dependsOn dokkaHtml
        archiveClassifier = "javadoc"
        from dokkaHtml.outputDirectory
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/hwolf/validation")
                credentials {
                    username = project.findProperty("gpr.user")
                    password = project.findProperty("gpr.key")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components.java
                artifact tasks.sourceJar
                //artifact tasks.javadocJar
            }
        }
    }
}
